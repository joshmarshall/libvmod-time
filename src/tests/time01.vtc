varnishtest "Test time returns appropriate value."

server s1 {
    rxreq
    txresp
} -start


varnish v1 -vcl+backend {
    import time from "${vmod_topbuild}/src/.libs/libvmod_time.so";

    sub vcl_deliver {
        set resp.http.x-time-timestamp = time.time("100.0", now);
        set resp.http.x-time-fallback = time.time("foobar", time.time("0", now));
        set resp.http.x-strftime-timestamp = time.strftime("%s", time.time("100.0", now));
        set resp.http.x-add-timestamp = time.add(time.time("0", now), 100s);
        set resp.http.x-sub-timestamp = time.sub(time.time("100", now), 100s);

        if (!time.is_after(time.time("0", now), time.time("1", now))) {
            set resp.http.x-is-after-false = "false";
        }
        if (time.is_after(time.time("1", now), time.time("0", now))) {
            set resp.http.x-is-after-true = "true";
        }
    }
} -start


client c1 {
    txreq -url "/"
    rxresp
    expect resp.http.x-time-timestamp == "Thu, 01 Jan 1970 00:01:40 GMT"
    expect resp.http.x-time-fallback == "Thu, 01 Jan 1970 00:00:00 GMT"
    expect resp.http.x-strftime-timestamp == "100"
    expect resp.http.x-add-timestamp == "Thu, 01 Jan 1970 00:01:40 GMT"
    expect resp.http.x-sub-timestamp == "Thu, 01 Jan 1970 00:00:00 GMT"
    expect resp.http.x-is-after-false == "false"
    expect resp.http.x-is-after-true == "true"
}

client c1 -run
